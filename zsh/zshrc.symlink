# load all of the environment variables
source ~/.envvars

# use .localrc for secret stuff or stuff that's specific to an
# individual machine. just anything that you don't want in your
# public repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# get all zsh files in our dotfiles directory
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
